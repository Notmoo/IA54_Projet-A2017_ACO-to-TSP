/** 
 * 
 */
package com.utbm.ia54.aco.Agent

import io.sarl.core.Initialize
import io.sarl.core.Logging
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Lifecycle
import com.utbm.ia54.aco.Updated
import com.utbm.ia54.aco.AntCreated
import com.utbm.ia54.aco.RequestUpdate
import com.utbm.ia54.aco.AntFinished
import com.utbm.ia54.aco.IterationFinished
import com.utbm.ia54.aco.SimpleIterationStart
import com.utbm.ia54.aco.TWUpdated
import com.utbm.ia54.aco.TWIterationStart

/** 
 * @author Mara
 */
agent Scheduler {
	uses Logging, DefaultContextInteractions, Lifecycle
	
	var nbAnts : int
	var nbAntsAck : int
	var nbAntsCreated : int

	on Initialize {
		loggingName = "Scheduler"
		
		if (!occurrence.parameters.isEmpty) {
			nbAnts = occurrence.parameters.get(0) as Integer
		}

		nbAntsAck = 0;
		nbAntsCreated = 0;
		
		for (var i = 0; i < nbAnts; i++) {
			spawn(AntAgent, i);
		}
	}
	
	on Updated {
		var env = occurrence.env
		var nbNodes = occurrence.nbNodes

		var newEvent = new SimpleIterationStart(nbNodes, env, (-1) as short, (-1) as short)
		emit(newEvent)
	}
	
	on TWUpdated {
		new TWIterationStart(
			occurrence.nbNodes,
			occurrence.env,
			 (-1) as short, 
			 (-1) as short, 
			 occurrence.timeWindows, 
			 occurrence.timeStamp
		).emit
	}
	
	on AntCreated {
		nbAntsCreated ++
		if (nbAntsCreated >= nbAnts) {
			nbAntsCreated = 0;
			emit(new RequestUpdate);	
		}
	}
	
	on AntFinished {
		nbAntsAck++
		
		if (nbAntsAck >= nbAnts) {
			nbAntsAck = 0
			emit(new IterationFinished)
		}
	}
}
