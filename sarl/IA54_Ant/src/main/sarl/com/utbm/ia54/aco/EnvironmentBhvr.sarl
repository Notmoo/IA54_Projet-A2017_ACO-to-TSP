package com.utbm.ia54.aco

import io.sarl.core.Logging
import io.sarl.core.Lifecycle
import io.sarl.core.DefaultContextInteractions
import io.sarl.core.Destroy
import io.sarl.core.Initialize
import com.utbm.ia54.aco.Agent.Scheduler
import java.util.UUID
import java.util.Map
import java.util.List

abstract behavior AbstractTSPEnvironment {
	uses Logging, Lifecycle, DefaultContextInteractions
	/********************************************************************** 
	 */
	protected var env : float[][][] // trois dim, a voir avec Cess pour implémenter ce qu'il a fait
	protected var nbNodes : short
	//TODO demander à Mara ce que c'est
	protected var Q = 1 // valeur a régler
	
	protected var defaultPheromoneLevel : float // Niveau initial de phéromones
	protected var pheromoneEvaporationFactor : float // Variable pour l'importance de l'évaporation des phéromones dans l'env
	protected var distanceFactor : float // Variable pour l'importance de la distance dans l'env
	
	
	protected var solution : float[][]
	protected var distanceMin : double

	/********************************************************************** 
	 */
	on Initialize {
		loggingName = "Environment"
		
		var nbAnts : int
		if (!occurrence.parameters.isEmpty) {
			nbAnts = occurrence.parameters.get(0) as Integer
			if (nbAnts <= 0) {
				error("Number of Ants specified but invalid, took 5")
				nbAnts = 5
			}
		}else{
			error("Number of Ants not specified, took 5")
			nbAnts = 5
		}

		spawn(Scheduler, nbAnts) // nbAnts à passer en paramètre
		distanceMin = -1
		Q = 1
		distanceFactor = 1f
		defaultPheromoneLevel = 1f
		pheromoneEvaporationFactor = 0.5f
		
	}

	on Destroy {
	}

	on RequestUpdate {
		sendToZero()
		if (env.isEmpty) {
			var senderID = occurrence.source.getUUID
			sendUpdateEvent(senderID)
		}
	}

	on TspPath {
		var senderID = occurrence.source.getUUID

		var newDist = occurrence.distance

		if (distanceMin === -1 || distanceMin > newDist) {
			distanceMin = newDist
		}

		updateSolution(occurrence.nodes, occurrence.nbNodes);

		emit(new AckSolution)[it.getUUID == senderID]
	}

	on IterationFinished {
		updateEnvironment()
	}

	on AntCreated {
		new AntCreatedAck().emit[it.UUID === occurrence.source.UUID]
	}
	
	on TspPropertyChanged {
		info("Property changed received : " + occurrence.property_id + " -> " + occurrence.property_value)
		switch occurrence.property_id {
			case "distance importance in env": {
				var temp = Float.parseFloat(occurrence.property_value)
				distanceFactor = temp
			}
			case "pheromone evaporation factor": {
				var temp = Float.parseFloat(occurrence.property_value)
				pheromoneEvaporationFactor = temp
			}
			
		}
	}
	/********************************************************************** 
	 */
	def updateSolution(nodes : short[], nbNode : short) {
		for (var i = 0; i < nbNode - 1; i++) {
			var beginNode = nodes.get(i)
			var finishNode = nodes.get(i + 1)

			var value = solution.get(beginNode).get(finishNode) + 1

			solution.get(beginNode).set(finishNode, value)
		}
	}

	def updateEnvironment() {
		for (var i = 0; i < nbNodes; i++) {
			for (var j = 0; j < nbNodes; j++) {
				env.get(i).get(j).set(1,
					pheromoneEvaporationFactor *
						(env.get(i).get(j).get(1) + distanceFactor * solution.get(i).get(j) / env.get(i).get(j).get(0)))
			}
		}
	}

	def sendToZero() {
		for (var i = 0; i < nbNodes; i++) {
			for (var j = 0; j < nbNodes; j++) {
				solution.get(i).set(j, 0f)
			}
		}
	}

	def abstract sendUpdateEvent()
	def abstract sendUpdateEvent(uuid : UUID)
}

behavior SimpleTSPEnvironment extends AbstractTSPEnvironment {
	uses DefaultContextInteractions
	
	def sendUpdateEvent {
		new Updated(env, nbNodes).emit
	}
	
	def sendUpdateEvent(uuid : UUID) {
		new Updated(env, nbNodes).emit[it.UUID === uuid]
	}

	on NewEnvironment [isFromMe] {
		sendToZero()
		env = occurrence.env
		nbNodes = occurrence.nbNodes
		distanceMin = 0

		sendUpdateEvent
	}
}

behavior TWTSPEnvironment extends AbstractTSPEnvironment {
	uses DefaultContextInteractions
	
	protected var timeWindow : Map<Short, List<Pair<Float, Float>>>
	protected var initialTime : float
	
	on Initialize {
		timeWindow = null
		initialTime = 0 as float
	}
	
	def sendUpdateEvent {
		new TWUpdated(env, nbNodes, timeWindow, initialTime).emit
	}
	
	def sendUpdateEvent(uuid : UUID) {
		new TWUpdated(env, nbNodes, timeWindow, initialTime).emit[it.UUID === uuid]
	}

	on NewTWEnvironment [isFromMe] {
		sendToZero()
		env = occurrence.env
		nbNodes = occurrence.nbNodes
		timeWindow = occurrence.timeWindow
		initialTime = occurrence.initialTimeStamp
		distanceMin = 0

		sendUpdateEvent
	}
}